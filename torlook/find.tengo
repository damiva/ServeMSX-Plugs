srv := import("server")
txt := import("text")
jsn := import("json")
url := "https://torlook.info/"
stg := srv.memory()
stg = is_array(stg) && len(stg) == 4 ? stg : ["seeders", false, false, false]
its := []
panic(srv.method != "POST" ? 400 : undefined)
fnd := jsn.decode(srv.read())
panic(is_error(fnd) ? fnd : !is_map(fnd) || !is_string(fnd.data) ? 400 : undefined)
fnd = fnd.data
r := srv.request(
    url + "?cinema=on&s=" + srv.encode_uri(fnd) + "&sort=" + stg[0] + (stg[1] ? "&forced=on" : ""),
    {header: {"User-Agent": "Mozilla/5.0 (X11; Linux x86_64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/97.0.4692.71 Safari/537.36"}}
)
panic(is_error(r) ? r : r.status != 200 ? r.status : undefined)
if r = txt.re_find("(?s)webResult.+?item.+?<a.+?>(.+?)</a>.+?<img.+?src=\"/(.+?)\".+?<a.+?>(.+?)<.+?class=\"size\">(.+?)<.+?class=\"date\">(.+?)<.+?class=\"arrow-up\".+?>(.+?)<.+?data-src=\"(.+?)\"", r.body, -1); is_array(r) {
    for i in r { its = append(its, {
        headline: txt.re_replace("<.+?>", i[1].text, ""),
        titleFooter: i[3].text + "{tb}{ico:msx-white:attach-file}" + txt.replace(i[4].text, "&nbsp;", " ", 1) +
            "{tb}{ico:msx-white:calendar-today} " + i[5].text + "{tb}{ico:msx-white:arrow-upward} " + i[6].text,
        data: url + srv.encode_uri(i[7].text, true)
    })}
} else {
    its = append(its, {label: "{dic:message:playlist_empty|Empty}", icon: "msx-yellow:warning", extensionIcon: "msx-white:refresh", action: "reload:content"})
}

srv.write(jsn.encode({response: {status: 200, data: {action: "content:data", data:{
    type: "list", headline: "Torlook", extension: "{ico:search}{col:msx-white}" + fnd, cache: false, compress: true, items: its, flag: "FIND",
    template: {type: "control", layout: "0,0," + (stg[2] ? "8" : "16") + ",2", action: "execute:" + srv.base_url + "magnet.tengo?"},
    header: {items:[
        {type: "control", icon: "arrow-back-ios", label: "TOP-50", action: "close:FIND", layout: "0,0,3,1"},
        {
            type: "control", icon: "search", extensionIcon: "keyboard", layout: "3,0,10,1", 
            id: "search", label: fnd, action: "execute:http://" + srv.host + "/msx/input",
            data: {action: "[cleanup|close:FIND|execute:" + srv.base_url + "find.tengo]", headline: "Torlook:", value: fnd}
        },{
            type: "button", icon: "tune", iconSize: "small", layout: "13,0,3,1", 
            action: "execute:info:dictionary:" + srv.base_url + "set.tengo?find=" + srv.encode_uri(fnd), data: undefined
        }
    ]}
}}}}))