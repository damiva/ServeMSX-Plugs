srv := import("server")
jsn := import("json")
stg := srv.memory()
stg = is_array(stg) && len(stg) == 3 ? stg : ["seeders", false, false]
if srv.method == "POST" {
    if b := jsn.decode(srv.read()); is_map(b) {
        if is_string(b.data) {
            stg[0] = b.data
        } else {
            stg[b.data ? 1 : 2] = !stg[b.data ? 1 : 2]
        } 
    }
    srv.memory(stg)
    srv.write(jsn.encode({response: {status: 200, data: {action: "reload:panel"}}}))
} else {
    r := {
        type: "list", headline: "Параметры поиска", extension: "Torlook", reuse: false, cache: false, restore: false,
        template: {type: "control", layout: "0,0,8,1", action: "execute:service:" + srv.base_url + "settings.tengo", enumerate: false},
        items: [{icon: "view-list", label: "представление списка", extensionLabel: "{ico:" + (stg[2] ? "" : "msx-white:") + "splitscreen} {ico:msx-white:toggle-o" + (stg[2] ? "n" : "ff") + "} {ico:" + (stg[2] ? "msx-white:" : "") + "grid-view}", data: false}]
    }
    for s in [["сидам", "seeders"], ["размеру", "size"], ["дате", "date"]] {
        r.items = append(r.items, {icon: "sort", label: "сортировать по " + s[0], extensionIcon: stg[0] == s[1] ? "msx-white:radio-button-on" : "radio-button-off", data: s[1]})
    }
    r.items = append(r.items,
        {icon: "manage-search", label: "форсированный поиск", extensionIcon: stg[1] ? "msx-white:check-box" : "check-box-outline-blank", data: true},
        {type: "button", label: "Обновить результаты поиска", action: "[back|reload:content]", offset: "2,0,-4,0"}
    )
    srv.write(jsn.encode(r))
}