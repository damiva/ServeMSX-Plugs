srv := import("server")
jsn := import("json")
txt := import("text")
url := "https://torlook.info/"
act := (srv.read("v") > "0.1.142" ? "replace:content:torlook:" : "content:") + srv.base_url + "?find="
its := []
stg := srv.memory()
stg = is_array(stg) && len(stg) == 2 ? stg : ["seeders", false]
get := func(s, p) {
    r := srv.request(
        url + (s  ? ("?cinema=on&s=" + srv.encode_uri(s) + "&sort=" + stg[0] + (stg[1] ? "&forced=on" : "")) : "movietop"),
        {header: {"User-Agent": "Mozilla/5.0 (X11; Linux x86_64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/97.0.4692.71 Safari/537.36"}}
    )
    return is_error(r) ? string(r) : r.status == 200 ? txt.re_find(p, r.body, -1) : r.status == 404 ? "Ничего не найдено!" : r.status > 499 ? (url + " не доступен!") : string(r.status)
}
fnd := srv.read("find")
rtn := ""
if fnd {
    if rtn = get(fnd, "(?s)webResult.+?item.+?<a.+?>(.+?)</a>.+?<img.+?src=\"/(.+?)\".+?<a.+?>(.+?)<.+?class=\"size\">(.+?)<.+?class=\"date\">(.+?)<.+?class=\"arrow-up\".+?>(.+?)<.+?data-src=\"(.+?)\""); is_array(rtn) {
        for i in rtn { its = append(its, {
            headline: txt.re_replace("<.+?>", i[1].text, ""),
            titleFooter: txt.join([i[3].text, txt.replace(i[4].text, "&nbsp;", " ", 1), "{ico:calendar-today} " + i[5].text, "{ico:arrow-upward} " + i[6].text], "{tb}{txt:msx-white:|}{tb}"),
            data: url + srv.encode_uri(i[7].text, true)
        })}
    }
} else if rtn = get("", "(?s)webResult.+?item.+?<p>.+?href=\"/(.+?)\">(.+?)<.+?torstat.+?<span.+?>(.+?)<"); is_array(rtn) {
    for i in rtn {its = append(its, {label: i[2].text, extensionLabel: i[5].text, icon: "search", action: act + i[1].text})}
}
if len(its) == 0 {
    its = append(its, {icon: "msx-yellow:warning", label: is_string(rtn) ? rtn : "Пусто!", extensionIcon: "msx-white:refresh", action: "reload:content"})
    rtn = false
} else {
    its[0].focus = fnd ? true : false
    rtn = true
}
srv.write(jsn.encode({
    type: "list", items: its, flag: "torlook", headline: "Torlook", compress: true,
    extension: fnd ? ("{ico:search} {col:msx-white}" + fnd) : "{col:msx-white}ТОП 50 (кинопоиск)", 
    template: {type: "control", layout: "0,0,16," + (rtn && fnd ? "2" : "1"), action: "execute:service:" + srv.base_url + "magnet.tengo"},
    header: {compress: true, items: [
        {type: "space", layout: "1,0,15,1", offset: "-1,0,1,0", color: "msx-black"},
        {type: "button", label: "ТОП 50", action: act, layout: "0,0,2,1"},
        {
            type: "control", icon: "search", extensionIcon: "keyboard", layout: "2,0,12,1",
            label: fnd || "{col:msx-white-soft}Поиск торрентов", focus: fnd ? false : true,
            action: "execute:http://" + srv.host + "/msx/input",
            data: {action: "[back|" + act + "{INPUT}]", headline: "Поиск торрентов", value: fnd, extension: "{ico:search}"}
        },
        {type: "button", icon: "settings", iconSize: "small", layout: "14,0,2,1", action: "panel:" + srv.base_url + "settings.tengo"}
    ]}
}))