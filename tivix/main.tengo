url := "http://tv.tivix.co/"
srv := import("server")
txt := import("text")
jsn := import("json")
stg := import("settings")
cat := stg.cat
fav := cat == "*"
cat = cat == "*" ? "" : cat
vid := "video:resolve:" + srv.base_url + "video.tengo?str=" + string(stg.str) + "&cid="

// get the page:
r := srv.request(url + cat)
panic(is_error(r) ? r : r.status != 200 ? r.status : undefined)

// parse categories:
c := [{label: "Избранные", action: "execute:fetch:" + srv.base_url + "set.tengo?id={ID}&cat=*", enable: !fav}]
for m in txt.re_find("(?s)class=\"menuuuuuu\">(.+?)</div>", r.body, 3) {
    for i in txt.re_find("(?s).+?href=\"/(.*?)\"(.+?)>(.+?)<", m[1].text, -1) {
        cc := {label: i[3].text, action: "execute:fetch:" + srv.base_url + "set.tengo?id={ID}&cat="+ i[1].text}
        if !fav && len(i[2].text) > 3 {
            cc.enable = false
            cat = (i[1].text == "" ? "" : "{col:msx-white}") + i[3].text
        }
        c = append(c, cc)
    }
}

// parse channels:
if fav {
    cat = "{col:msx-yellow}Избранные"
    if r = srv.file("favs.json"); r {
        panic(is_error(r) ? r : undefined)
        r = jsn.decode(r)
        panic(is_error(r) ? r : !is_array(r) ? "Wrong format of file favs.json" : undefined)
        for i := 0; i < len(r); i++ {
            r[i].playerLabel = r[i].headline
            r[i].action = vid + r[i].id
        }
    } else { r = [] }
} else if r = txt.re_find("(?s)class=\"all_tv\".+?<a.+?href=\".+?//.+?/(.+?).html\".+?title=\"(.+?)\".+?src=\"/(.+?)\"", r.body, -1); r { 
    for i := 0; i < len(r); i++ {
        r[i] = {id: r[i][1].text, headline: r[i][2].text, playerLabel: r[i][2].text, image: url + r[i][3].text, action: vid + r[i][1].text}
    }
} else { r = [] }

// msx answer:
props := srv.settings.player(true)
props["trigger:load"] = "execute:service:fetch:" + srv.base_url + "programme.tengo?cid={context:id}"
props["control:type"] = "extended"
props["progress:type"] = "time:hh:mm"
props["label:extension"] = " {ico:msx-white:access-time} {now:time:hh:mm}"
props["tizen:display:mode"] = "PLAYER_DISPLAY_MODE_FULL_SCREEN"
opt := fav ? "Убрать из избранных" : "Добавить в избранные"
r = jsn.encode({
    type: "list", headline: "Tivix",
    compress: true, items: r,
    extension: "{ico:filter-list} " + cat + "{tb}{ico:msx-white-soft:access-time} {txt:msx-white:now:time:hh:mm}",
    header: {items: [
        {
            type: "control", icon: "filter-list", label: "Категория:", extensionLabel: cat, layout: "2,0,6,1", action: "panel:data", 
            data: {type: "list", items: c, headline: "Категории", template: {type: "button", layout: "0,0,4,1"}}
        },{
            type: "control", icon: "stream", label: "Видео:", extensionLabel: stg.hls[stg.str], layout: "8,0,6,1",
            action: "panel:" + srv.base_url + "set.tengo?id={ID}"
        }
    ]},
    template: {
        layout: "0,0,8,2", truncation: "titleHeader|titleFooter",
        imageWidth: 1, imageFiller: "smart", 
        progress: -1, progressColor: "msx-white", titleHeader: "", titleFooter: "",
        live: {type: "setup", action: "execute:service:fetch:" + srv.base_url + "programme.tengo?cid={context:id}&now=true"},
        properties: props
    }, options: {
        headline: "{dic:caption:options|options}:", 
        caption: "{dic:caption:options|options}:{tb}{ico:msx-green:stop} " + opt + "{tb}{ico:msx-yellow:stop} {dic:Up|Up}",
        template: {type: "control", layout: "0,0,8,1"}, 
        items:[
            {key: "green", icon: "msx-green:stop", label: opt, action: "execute:fetch:" + srv.base_url + "set.teng?cid={context:id}" + (fav ? "" : "&ttl={context:headline}&img={context:image}")},
            {key: "yellow", icon: "msx-yellow:stop", label: "{dic:Up|Up}", action: "[cleanup|focus:index:0]"},
            {type: "space"}, {icon: "menu", label: "{dic:caption:menu|Menu}", action: "menu"}
        ]
    }
})
panic(is_error(r) ? "Encoding playlist error: " + string(r) : undefined)
srv.write(r)